apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'signing'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    google()
    jcenter()
    maven { url "https://jitpack.io" }
}

String mVersionName = "1.1.7"

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"


    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 9
        versionName mVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'io.github.WeLoopTeam'
                artifactId = 'weloop'
                version = mVersionName
                from components.release
                pom {
                    signing {
                        sign publishing.publications.release
                        sign configurations.archives
                    }
                    name = 'WeLoop'
                    description = 'WeLoop Android sdk'
                    url = 'https://weloop.io'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'alexandre'
                            name = 'Alexandre Thauvin'
                            email = 'alexandre@thauvin.dev'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/WeLoopTeam/WeLoop-Android.git'
                        developerConnection = 'scm:git:ssh://github.com/WeLoopTeam/WeLoop-Android.git'
                        url = 'https://github.com/WeLoopTeam/WeLoop-Android'
                    }
                }
            }
        }
        repositories {
            maven {
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                credentials {
                    username = properties.getProperty('mavenCentralUsername')
                    password = properties.getProperty('mavenCentralPassword')
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'com.google.android.material:material:1.4.0'


    def retrofit_version = "2.9.0"
    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"


    //alert dialog
    implementation 'com.github.f0ris.sweetalert:library:1.5.6'

    def glide_version = "4.12.0"
    //glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:4.12.0"

    //okHttp
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    //rxjava
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
