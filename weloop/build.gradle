apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'signing'
apply plugin: 'maven-publish'

repositories {
    maven { url "https://plugins.gradle.org/m2/" }
    mavenCentral()
    google()
    maven { url "https://jitpack.io" }
}

String mVersionName = "2.1.5"

android {
    defaultConfig {
        compileSdk 34
        minSdkVersion 21
        targetSdkVersion 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }

    buildFeatures {
        viewBinding = true
    }
    namespace 'com.weloop.weloop'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'io.github.WeLoopTeam'
                artifactId = 'weloop'
                version = mVersionName
                from components.release
                pom {
                    signing {
                        sign publishing.publications.release
                        sign configurations.archives
                    }
                    name = 'WeLoop'
                    description = 'WeLoop Android sdk'
                    url = 'https://weloop.io'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'alexandre'
                            name = 'Alexandre Thauvin'
                            email = 'alexandre@thauvin.dev'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/WeLoopTeam/WeLoop-Android.git'
                        developerConnection = 'scm:git:ssh://github.com/WeLoopTeam/WeLoop-Android.git'
                        url = 'https://github.com/WeLoopTeam/WeLoop-Android'
                    }
                }
            }
        }
        repositories {
            maven {
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                credentials {
                    username = properties.getProperty('mavenCentralUsername')
                    password = properties.getProperty('mavenCentralPassword')
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'com.google.android.material:material:1.11.0'

    implementation 'me.pushy:sdk:1.0.103'

    implementation 'com.jakewharton.timber:timber:5.0.1'

    def retrofit_version = "2.9.0"
    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"


    //alert dialog
    implementation 'com.github.f0ris.sweetalert:library:1.5.6'

    def glide_version = "4.12.0"
    //glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:4.12.0"

    //okHttp
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
}
