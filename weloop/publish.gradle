apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group 'com.github.WeLoopTeam'
version '1.1.1'

project.ext {
    mavGitUrl = 'https://github.com/WeLoopTeam/WeLoop-Android.git'
    mavProjectName = 'WeLoop'
    mavLibraryLicenses = ["MIT License":'https://github.com/WeLoopTeam/WeLoop-Android/blob/master/LICENSE']
    mavLibraryDescription = "WeLoop for Android"
    mavPublishToMavenLocal = true
    mavSiteUrl = 'https://github.com/WeLoopTeam/WeLoop-Android'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

//task javadoc(type: Javadoc) {
//    source = android.sourceSets.main.java.srcDirs
//    classpath = project.files(android.getBootClasspath().join(''))
//}
//
//task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc'
//    from javadoc.destinationDir
//}

artifacts {
 //   archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact sourcesJar
            artifact("$buildDir/outputs/aar/weloop-release.aar")
            groupId 'com.github.WeLoopTeam'
            artifactId 'weloop'
            version this.version
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}


def properties = new Properties()
properties.load(new FileInputStream("local.properties"))

bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApikey")
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'WeLoop'
        name = 'WeLoop'
        description = "WeLoop for Android"
        publicDownloadNumbers = false
        licenses = ['MIT']
        override = true
        vcsUrl = 'https://github.com/WeLoopTeam/WeLoop-Android.git'
        version {
            name = this.version
            desc = "Version ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}

apply from: 'https://raw.githubusercontent.com/sky-uk/gradle-maven-plugin/master/gradle-mavenizer.gradle'